{{{
	IniTest.kn v0.5 (for Kuin 1.01):
		Last Modified: 2014/02/16 01:18:27.
		Created by @tatt61880
			https://twitter.com/tatt61880
			https://github.com/tatt61880
}}}

const Filename: []char :: "IniTest_data1.knd"
var You: @CHuman
var Rival: @CHuman
var Kuina: @CHuman
var Ini: Ini@CIni

func Main()
	do @Ini :: (#Ini@CIni).Init(@Filename)
	do @You :: (#@CHuman).Init("あなた", false)
	do @Rival :: (#@CHuman).Init("ライバル", false)
	do @Kuina :: (#@CHuman).Init("くいなちゃん", true)
	if(Load() = false)
		do @You :: (#@CHuman).Init("あなた", false)
		do @Rival :: (#@CHuman).Init("ライバル", false)
		do @Kuina :: (#@CHuman).Init("くいなちゃん", true)
		do Save()
		do Dbg@Log("セーブデータを新規作成しました。")
	else
		do Dbg@Log("セーブデータを読み込みました。")
	end if

	var temp: []char :: SaveData@Load(@Filename) $ []char
	do Dbg@Log("セーブデータの中身[[[\n" ~ temp ~ "\n]]]\n")
	do Dbg@Log("★" ~ @You.level.ToStr())
	do @You.LevelUp()
	do Dbg@Log("★" ~ @You.level.ToStr())
	do @Rival.LevelUp()
	do Dbg@Log("セーブデータを保存します。\n")
	do Save()
	do temp :: SaveData@Load(@Filename) $ []char
	do Dbg@Log("セーブデータの中身[[[\n" ~ temp ~ "\n]]]\n")

	func Save()
		do SaveStatus(@You)
		do SaveStatus(@Rival)
		func SaveStatus(human: @CHuman)
			do @Ini.SetInt(human.name, "レベル", human.level)
			do @Ini.SetInt(human.name, "強さ", human.strength)
			do @Ini.SetInt(human.name, "賢さ", human.cleverness)
			do @Ini.SetInt(human.name, "逞しさ", human.hardiness)
		end func
	end func
	func Load(): bool
		var loadtest: []char
		var temp: []byte8 :: SaveData@Load(@Filename)
		if(temp =& null)
			return false
		end if
		do loadtest :: temp $ []char
		var result: bool :: LoadStatus(@You) & LoadStatus(@Rival)
		if(result = false)
			do Dbg@Log("お気の毒ですが、セーブデータが破損しています。")
			return false
		end if
		return true

		func LoadStatus(human: @CHuman): bool
			do human.level :: @Ini.GetInt(human.name, "レベル", -1)
			do Dbg@Log("★" ~ human.name ~ " " ~ human.level.ToStr())
			do human.strength :: @Ini.GetInt(human.name, "強さ", -1)
			do human.cleverness :: @Ini.GetInt(human.name, "賢さ", -1)
			do human.hardiness :: @Ini.GetInt(human.name, "逞しさ", -1)
			if(human.level = -1 | human.strength = -1 | human.cleverness = -1 | human.hardiness = -1)
				return false
			else
				return true
			end if
		end func
	end func
end func

class CHuman()
	var name: []char
	var strength: int
	var cleverness: int
	var hardiness: int
	var level: int
	func Init(name: []char, isKuina: bool): @CHuman
		do me.name :: name
		if(isKuina)
			do me.strength :: inf $ int
			do me.cleverness :: inf $ int
			do me.hardiness :: inf $ int
			do me.level :: inf $ int
		else
			do me.strength :: 10
			do me.cleverness :: 20
			do me.hardiness :: 1
			do me.level :: 1
		end if
		return me
	end func
	func LevelUp()
		do Dbg@Log("♪♪" ~ me.name ~ "がレベルアップしました♪♪")
		{
		do me.strength :+ Rnd@Get(2, 5)
		do me.cleverness :+ Rnd@Get(5, 10)
		do me.hardiness :+ Rnd@Get(1, 3)
		}
		do me.strength :+ 10
		do me.cleverness :+ 20
		do me.hardiness :+ 30
		do me.level :+ 1
	end func
end class
